#+TITLE: Convert =YAML= to JACoW Template
#+AUTHOR: Joshua Peters, Zhichu Chen (Ross)

#+STARTUP: show3levels

#+EXPORT_FILE_NAME: index
#+PROPERTY: header-args    :noweb tangle :tangle no :exports code :eval no :mkdirp yes

#+OPTIONS: html-style:nil
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="https://fniessen.github.io/org-html-themes/src/readtheorg_theme/css/htmlize.css"/>
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="https://fniessen.github.io/org-html-themes/src/readtheorg_theme/css/readtheorg.css"/>
#+HTML_HEAD: <script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
#+HTML_HEAD: <script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.4/js/bootstrap.min.js"></script>
#+HTML_HEAD: <script type="text/javascript" src="https://fniessen.github.io/org-html-themes/src/lib/js/jquery.stickytableheaders.min.js"></script>
#+HTML_HEAD: <script type="text/javascript" src="https://fniessen.github.io/org-html-themes/src/readtheorg_theme/js/readtheorg.js"></script>

* Introduction

“Author Title Check” is a tedious job during JACoW proceedings preparations. This tool intends to provide a tool to automatically generate the template for the authors when they submit their abstracts.

Ideally, the web server can collect the information of the title, the author list, and the corresponding affiliation list of each author. Let’s assume it can export the configuration to a =YAML= file, what we need now is to parse it and export a template that can be directly used by the authors.

There’re three things need to be clarify:
- It doesn’t have to be =YAML=. Any format will do as long as it can be parsed. =JSON= is fine. =XML= needs additional efforts for the code because another library will be used.
- There are two variants of the author-affiliation list described in the [[https://github.com/JACoW-org/JACoW_Templates][template]]. Ross thought one of them is ambiguous so there will be no option to choose it in this version.
- Right now, this will only focus on LaTeX.

* Configuration

The =YAML= file is supposed to have the following form:
#+begin_src yaml
  title:
    caption: This is the CAPTION
    funds:
      - Funded by A
      - Funded by B
  authors:
  - name:
      firstName: John
      lastName:  Doe
      otherFields: will be ignored
    type: primary
    email: john.doe@first.author
    address:
    - name: Inst. A
      address: Addr. A
      zip: '123456'
#+end_src

** Datatype definitions
:PROPERTIES:
:header-args: :mkdirp yes
:END:

*** Main file
:PROPERTIES:
:header-args: :tangle JACoW/Types.hs :noweb tangle
:END:

This library will be imported by my program.

Use pragmas
#+begin_src haskell
  <<type-pragma>>
#+end_src

#+begin_src haskell
  module JACoW.Types where
#+end_src

Import libraries
#+begin_src haskell
  <<type-imports>>
#+end_src

The =Prelude= library will be imported at the end to ensure everything is already in scope and we will have no warnings:
#+begin_src haskell
  import Prelude hiding
         (
           <<type-prelude-hide>>
         )
#+end_src

Create typeclasses
#+begin_src haskell
  <<type-class>>
#+end_src

Define handy functions
#+begin_src haskell
  <<type-function>>
#+end_src

And our data type and corresponding typeclasses
#+begin_src haskell
  <<type-defs>>
#+end_src

*** Pragma
:PROPERTIES:
:header-args: :noweb-ref type-pragma
:END:

Automatically convert =String= inside double quotation marks to =Text=
#+begin_src haskell
  {-# LANGUAGE OverloadedStrings #-}
#+end_src

*** Type class
:PROPERTIES:
:header-args: :noweb-ref type-class
:END:

Define a class that allows data be converted to LaTeX strings:
#+begin_src haskell
  class LaTeXable a where
      fromtitle :: a -> [Affiliation] -> [LaTeX]
      tolatex   :: a -> [LaTeX]
#+end_src
This =totitle= function takes the data and the affiliation list, then returns the LaTeX objects.


*** LaTeX data type
:PROPERTIES:
:header-args: :noweb-ref type-defs
:END:


Let’s first define a bunch of LaTeX data types:
#+begin_src haskell
  data LaTeX = Raw String
             | Sup String
             | Maþ String
             | Mac String [LaTeX]
             | Spc
             | NewLine
             | TeXLine
             deriving Show
#+end_src
with corresponding LaTeX representation:

**** LaTeX functions
:PROPERTIES:
:header-args: :noweb-ref type-function
:END:

#+begin_src haskell
  stringify :: LaTeX -> String

  stringify (Raw s) = s
  stringify (Sup s) = supTeX s
  stringify (Maþ s) = mathTeX s
  stringify (Mac c ls) = cs1 c $ foldl1 (<>) $ stringify <$> ls
  stringify Spc = " "
  stringify NewLine = "\n"
  stringify TeXLine = "\\\\"
#+end_src

Since I will be using LaTeX macros a lot, Let me define a one-argument LaTeX macro:
#+begin_src haskell
  cs1 :: String -> String -> String
  cs1 cs arg1 = "\\" <> cs <> wrap "{" "}" arg1
  wrap :: String -> String -> String -> String
  wrap l r s = l <> s <> r
#+end_src
Now I can define:
#+begin_src haskell
  supTeX    = cs1 "textsuperscript"
  mathTeX   = wrap "$" "$"
#+end_src

#+begin_src haskell
  -- sep :: String -> [LaTeX] -> String
  -- sep s ltxs = foldl1 (<> s <>) (fmap toLaTeX ltxs)
#+end_src

To simplify the output, we can combine some elements together.
#+begin_src haskell
  simplify :: [LaTeX] -> [LaTeX]

  simplify [] = []
  simplify (Mac cs ls:xs) = (Mac cs (simplify ls)):simplify xs
  simplify (x:[]) = [x]
  simplify (Raw x1:Raw x2:xs) = simplify ((Raw (x1<>x2)):xs)
  simplify (Sup x1:Sup x2:xs) = simplify ((Sup (x1<>x2)):xs)
  simplify (Maþ x1:Maþ x2:xs) = simplify ((Maþ (x1<>x2)):xs)
  simplify (x:xs) = x:simplify xs
#+end_src

*** AuthorTitle
:PROPERTIES:
:header-args: :noweb-ref type-defs
:END:


The =YAML= configuration consists of two entries: a title, and an author list:
#+begin_src haskell
  data AuthorTitle = AuthorTitle
                     { title   :: Title
                     , authors :: [Author]
                     } deriving (Show)
#+end_src

Let me make a trivial instance to be used as a default value:
#+begin_src haskell
  emptyAuthorTitle = AuthorTitle (Title "" Nothing) [Author (Name "" "" Nothing) "" Nothing False]
#+end_src

We need to read the =title= entry to the =title= field (first one) and the =authors= entry to the =authors= field (second one):
#+begin_src haskell
  instance FromJSON AuthorTitle where
    parseJSON (Object v) =
      AuthorTitle    <$>
      v .:   "title" <*>
      v .:   "authors"
    parseJSON _ = fail "Expected Object for Config value"
#+end_src
=FromJSON= and other stuffs need to be imported from =Data.Yaml=:
#+begin_src haskell :noweb-ref type-imports
  import Data.Yaml
#+end_src
and these fancy operators =<$>=, =<*>= from =Control.Applicative=:
#+begin_src haskell :noweb-ref type-imports
  import Control.Applicative
#+end_src

Converting =AuthorTitle= to LaTeX is easy: converting both =title= and =authors=, then stick them together with a newline character:
#+begin_src haskell
  instance LaTeXable AuthorTitle where
    tolatex (AuthorTitle t as)  = intercalate [NewLine] [tolatex t,[Mac "author" (intercalate [TeXLine,NewLine] ([authorlist] <> affiliations ))]]
      where
        authorlist = intercalate [NewLine] authors
        authors = flip fromtitle affs <$> as
        affs = fromMaybe [] (nub <$> (foldl1 (<>) $ addresses <$> as))
        affiliations = toAff <$> (zip [1..] affs)
        toAff (n,aff) = [Sup (show n)] <> fromtitle aff affs
    fromtitle t _ = tolatex t
#+end_src
where =nub= removes duplicates in a list, =intersperse= inserts an element to a list and =intercalate= links the elements in a list with a given object. They all come from =Data.List=:
#+begin_src haskell :noweb-ref type-imports
  import Data.List
#+end_src

=fromMaybe= needs to be imported from =Data.Maybe= library:
#+begin_src haskell :noweb-ref type-imports
  import Data.Maybe
#+end_src

=authorTeX= is used to generate =\author{...}=:
#+begin_src haskell :noweb-ref type-function
  authorTeX  = cs1 "author"
#+end_src

=unTeXlines= puts =\\= at the end of each line:
#+begin_src haskell :noweb-ref type-function
  unTeXlines = intercalate ("\\\\" <> singleton '\n')
#+end_src

**** Title

The =Title= data type consists of a caption and probably a list of funds:
#+begin_src haskell
  data Title = Title
               { caption :: String
               , funds   :: Maybe [Fund]
               } deriving (Show)
#+end_src
where =Fund= is an alias of =Text= for now:
#+begin_src haskell
  type Fund = String
#+end_src

The =FromJSON= instance is straightforward:
#+begin_src haskell
  instance FromJSON Title where
    parseJSON (Object v) =
      Title            <$>
      v .:   "caption" <*>
      v .:?  "funds"
    parseJSON _ = fail "Expected Object for Config value"
#+end_src
and converting =Title= to LaTeX is put the caption and the list of funds in the =\title= micro:
#+begin_src haskell -l "--((ref:%s))"
  instance LaTeXable Title where
    tolatex (Title cap fs)  = [Mac "title" $ [Raw cap] <> intersperse (Sup ",") (fromMaybe [] $ ((Mac "thanks").singleton.(Raw)) <<$>> fs)] --((ref:sup-comma-in-title))
    fromtitle t _ = tolatex t
#+end_src
=<<$>>= is an alias of =fmap . fmap=:
#+begin_src haskell :noweb-ref type-function
  infixl 4 <<$>>
  (<<$>>) :: (Functor f, Functor g) => (a -> b) -> f (g a) -> f (g b)
  (<<$>>) = fmap . fmap
#+end_src


**** Author

=Author= has the following fields: a name, an email, probably a list of affiliations and whether the author is a primary author.
#+begin_src haskell
  data Author = Author
                { name       :: Name
                , email      :: Email
                , addresses  :: Maybe [Affiliation]
                , isPrimary  :: Bool
                } deriving (Show)
#+end_src

#+begin_src haskell
  instance FromJSON Author where
    parseJSON (Object v) =
      Author             <$>
      v .:   "name"      <*>
      v .:   "email"     <*>
      v .:?  "addresses" <*>
      (maybePrimary <$> (v .:? "type" .!= Just "other" :: Parser (Maybe String)))
    parseJSON _ = fail "Expected Object for Config value"
#+end_src

The =maybePrimary= function will check whether the =type= field in the =YAML= configuration is =primary= (case insensitive):
#+begin_src haskell :noweb-ref type-function
  maybePrimary :: Maybe String -> Bool
  maybePrimary (Just str) = (== "primary") $ toLower <$> str
  maybePrimary _ = False
#+end_src
and you need to import =toLower= from =Data.Char=:
#+begin_src haskell :noweb-ref type-imports
  import Data.Char
#+end_src

Converting =Author= to LaTeX will really use the affiliation list this time.
#+begin_src haskell -l "--((ref:%s))"
  instance LaTeXable Author where
    fromtitle (Author nm em addrs isP) affs = fromtitle nm affs <> intersperse (Sup ",") (thankslst <> catMaybes (fmap (flip addrIndex affs) (fromMaybe [] addrs))) --((ref:sup-comma-in-author))
      where thankslst =
              if isP
              then tolatex em
              else []
    tolatex as = fromtitle as []
#+end_src

#+RESULTS:
: <interactive>:8:16-24: error:
:     Variable not in scope: fromtitle :: t1 -> [a0] -> t2

=addrIndex= returns the index of the affiliation if found in the list:
#+begin_src haskell :noweb-ref type-function
  addrIndex :: Affiliation -> [Affiliation] -> Maybe LaTeX
  addrIndex aff affs = ((Sup).show.(+1)) <$> aff `elemIndex` affs
#+end_src

=Email= is an alias of =Text=:
#+begin_src haskell
  type Email = String
#+end_src
and is easy to be converted to LaTeX:
#+begin_src haskell
  instance LaTeXable Email where
    tolatex eml = [Mac "thanks" [Raw eml]]
    fromtitle e _ = tolatex e
#+end_src

=Name= and =Affiliation= is more complicated.

***** Name

=Name= has fields of =firstName=, =lastName= and probably =initials=:
#+begin_src haskell
  data Name = Name
              { firstName :: String
              , lastName  :: String
              , initials  :: Maybe String
              } deriving (Show)
#+end_src
and reads directly from:
#+begin_src haskell
  instance FromJSON Name where
    parseJSON (Object v) =
      Name               <$>
      v .:   "firstName" <*>
      v .:   "lastName"  <*>
      v .:?  "initials"
    parseJSON _ = fail "Expected Object for Config value"
#+end_src

=firstName lastName= would be the LaTeX representation:
#+begin_src haskell
  instance LaTeXable Name where
    tolatex (Name fnm lnm ini) = [Raw (unwords [fnm,lnm])] -- firstname lastname
    fromtitle n _ = tolatex n
#+end_src


***** Affiliation

=Affiliation= has it name and probably an address and a zip code:
#+begin_src haskell
  data Affiliation = Affiliation
                     { affName  :: String
                     , adress   :: Maybe String
                     , zipcode  :: Maybe String
                     } deriving (Show,Eq)
#+end_src
The reason I define =Affiliation= as an =Eq= class is because we need to locate the affiliation by comparing them.
#+begin_src haskell
  instance FromJSON Affiliation where
    parseJSON (Object v) =
      Affiliation      <$>
      v .:   "name"    <*>
      v .:?  "address" <*>
      v .:?  "zip"
    parseJSON _ = fail "Expected Object for Config value"
#+end_src
Append the address and/or the zip code when they are available:
#+begin_src haskell
  instance LaTeXable Affiliation where
    tolatex aff = [Raw (affName aff                                                    -- name
                        <> fromMaybe "" (((<>) ", ") <$> adress aff)                   -- , address
                        <> fromMaybe "" (((<>) ", ") <$> zipcode aff)                  -- , zip
                       )]
    fromtitle aff _ = tolatex aff
#+end_src

** YAML generation

In case I could make mistakes in the data type codes in =Haskell=, I will use =Python= to generate the configuration file to make it independent.

#+begin_src python :noweb tangle :tangle py-to-yml.py :shebang #!/usr/bin/env python
  import yaml

  class JACoW:
      pass

  mypaper = {}
  mypaper['title'] = {
      'caption': 'This is a title',
      'funds': ['Fund A', 'Fund B']
  }

  inst1 = {
      'name': 'Inst. A',
      'address': 'Somewhere',
      'zip': '121212'
  }

  inst2 = {
      'name': 'Inst. B',
      'address': 'Somewhere',
      'zip': '131313'
  }

  inst3 = {
      'name': 'Inst. C',
      'address': 'Somewhere',
      'zip': '141414'
  }

  author1 = {
      'name': {
          'firstName': 'John',
          'lastName': 'Doe',
          'initials': 'J.',
          'dateOfBirth': 'whocares'
             },
      'type': 'primary',
      'email': 'john.doe@first.author',
      'addresses': [ inst1, inst2, inst3 ]
  }

  author2 = {
      'name': {
          'firstName': 'Jane',
          'lastName': 'Doe',
          'initials': 'J.',
          'dateOfBirth': 'whocares'
             },
      'type': 'secondary',
      'email': 'jane.doe@second.author',
      'addresses': [ inst2, inst3 ]
  }

  author3 = {
      'name': {
          'firstName': 'Jack',
          'lastName': 'Doe',
          'initials': 'J.',
          'dateOfBirth': 'whocares'
             },
      'email': 'jack.doe@third.author',
      'addresses': None
  }

  author4 = {
      'name': {
          'firstName': 'Judy',
          'lastName': 'Doe',
          'initials': 'J.',
          'dateOfBirth': 'whocares'
             },
      'type': 'Primary',
      'email': 'judy.doe@fourth.author',
      'addresses': [ inst1, inst3 ]
  }

  mypaper['authors'] = [author1,author2,author3,author4]

  mypaper_yaml = yaml.safe_dump(mypaper,sort_keys=False)

  with open('mypaper.yaml', 'wt') as mypaper_info:
      mypaper_info.write(mypaper_yaml)

#+end_src



After running the script:
#+begin_src shell :results silent
  ./py-to-yml.py
#+end_src

Now the =YAML= file looks like this:
#+begin_src shell :results raw replace :wrap src yaml :exports both
  cat mypaper.yaml
#+end_src

#+RESULTS:
#+begin_src yaml
title:
  caption: This is a title
  funds:
  - Fund A
  - Fund B
authors:
- name:
    firstName: John
    lastName: Doe
    initials: J.
    dateOfBirth: whocares
  type: primary
  email: john.doe@first.author
  addresses:
  - &id003
    name: Inst. A
    address: Somewhere
    zip: '121212'
  - &id001
    name: Inst. B
    address: Somewhere
    zip: '131313'
  - &id002
    name: Inst. C
    address: Somewhere
    zip: '141414'
- name:
    firstName: Jane
    lastName: Doe
    initials: J.
    dateOfBirth: whocares
  type: secondary
  email: jane.doe@second.author
  addresses:
  - *id001
  - *id002
- name:
    firstName: Jack
    lastName: Doe
    initials: J.
    dateOfBirth: whocares
  email: jack.doe@third.author
  addresses: null
- name:
    firstName: Judy
    lastName: Doe
    initials: J.
    dateOfBirth: whocares
  type: Primary
  email: judy.doe@fourth.author
  addresses:
  - *id003
  - *id002
#+end_src

* Application

#+begin_src haskell :tangle yml2tex.hs
  module Main where

  import JACoW.Types
  import qualified Data.Yaml as Y
  --import qualified Data.Text.IO as TIO
  import qualified Data.ByteString.Char8 as BS

  simpletitle at = foldl1 (<>) $ stringify <$> (simplify $ tolatex at)

  main = do
    ymlData <- BS.readFile "mypaper.yaml"
    let authortitle = Y.decodeEither' ymlData :: Either Y.ParseException AuthorTitle
    writeFile "mypaper-hs.tex" $ either (error . show) simpletitle authortitle
#+end_src


You can run the script without compiling it:
#+begin_src shell :eval yes
  runhaskell yml2tex.hs
#+end_src

#+RESULTS:

And the result:
#+begin_src shell :results raw replace :wrap src latex :exports both :eval yes
  cat mypaper-hs.tex
#+end_src

#+RESULTS:
#+begin_src latex
\title{This is a title\thanks{Fund A}\textsuperscript{,}\thanks{Fund B}}
\author{John Doe\thanks{john.doe@first.author}\textsuperscript{,1,2,3}
Jane Doe\textsuperscript{2,3}
Jack Doe
Judy Doe\thanks{judy.doe@fourth.author}\textsuperscript{,1,3}\\
\textsuperscript{1}Inst. A, Somewhere, 121212\\
\textsuperscript{2}Inst. B, Somewhere, 131313\\
\textsuperscript{3}Inst. C, Somewhere, 141414}
#+end_src

* Debug

#+begin_quote
ghci> :l JACoW.Types
[1 of 1] Compiling JACoW.Types      ( JACoW/Types.hs, interpreted )
Ok, one module loaded.
ghci> import qualified Data.Yaml as Y
ghci> import qualified Data.ByteString.Char8 as BS
ghci> :m + Data.Either
ghci> ymlData <- BS.readFile "mypaper.yaml"
ghci> let authortitle = Y.decodeEither' ymlData :: Either Y.ParseException AuthorTitle
ghci> let a=fromRight emptyAuthorTitle authortitle
ghci> let b=tolatex a
#+end_quote

* Summary

The library looks fine. the =\textsuperscript{,}= inserted at [[(sup-comma-in-title)][here]] and [[(sup-comma-in-author)][here]] on the other hand is *so* ugly! This should be implemented in the LaTeX class file so that the document is more comprehensible.

